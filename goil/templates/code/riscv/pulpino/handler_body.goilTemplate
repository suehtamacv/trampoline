.global ISR_% !handlerSource %
.extern % !handlerIRQ %
.extern tpl_leave_ie_untouched
.extern push_fifo_it_masks
.extern tpl_mestatus

/*---------------------------------------------------------------------------*/
ISR_% !handlerSource %:

    /*-------------------------------------------------------------------------
     * 1 - Interrupt function call
     */
    jal % !handlerIRQ %

    /* Disable interruptions by default */
    csrci 0x7c0, 1

    /* No context switch if reentrant interruption */
    la a5, tpl_reentrancy_counter
    mv a6, a5
    lw a5, 0(a5)
    addi a5, a5, -1
    sw a5, 0(a6)
    bnez a5, 1f

    /*-------------------------------------------------------------------------
     * 2 - Check the context switch condition in tpl_kern
     */
    la a0, tpl_kern
    lb a6, TPL_KERN_OFFSET_NEED_SWITCH(a0)
    beq a6, zero, 1f

    /*-------------------------------------------------------------------------
     * 3 - Save context
     *     Load in a0 the pointer to the static descriptor of the running task
     *     a0 already points to tpl_kern (from stage 2)
     */
    lw  a0, TPL_KERN_OFFSET_S_RUNNING(a0)
    lw  a0, 0(a0)
    jal tpl_save_context

    /*-------------------------------------------------------------------------
     * 4 - Call tpl_run_elected.
     *     The argument is a boolean which is true if the process is
     *     preempted. Here it is true because in an interrupt handler
     *     a process is inevitably preempted
     */
    li a0, 1
    jal tpl_run_elected

    /*-------------------------------------------------------------------------
     * 5 - Load context
     *     Load in a0 the pointer to the context of the running task.
     *     It has been changed by tpl_run_elected
     */
    la  a0, tpl_kern
    lw  a0, TPL_KERN_OFFSET_S_RUNNING(a0)
    lw  a0, 0(a0)
    jal tpl_load_context

1:
% if category == 2 then %
    /* set priority */
    li  a0, % !priority %
    jal tpl_set_priority
    jal push_fifo_it_masks
% end if %
    /* Reenables interruptions */
    lw a6, tpl_reentrancy_counter
    bnez a6, 1f
    lw a6, tpl_mestatus
    csrw 0x7c0, a6
1:
    j tpl_no_context_switch
